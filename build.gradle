buildscript {
    ext.reaktive_version = '1.1.8'
    ext.reaktive_group_id = 'com.badoo.reaktive'
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

void setupMultiplatformLibrary(Project project) {
    project.apply plugin: 'org.jetbrains.kotlin.multiplatform'
    project.apply plugin: 'maven-publish'
    project.apply plugin: DetektPlugin

    project.group = reaktive_group_id
    project.version = reaktive_version

    project.kotlin {
        sourceSets {
            commonMain {
                dependencies {
                    implementation Deps.kotlin.stdlib.common
                }
            }

            commonTest {
                dependencies {
                    implementation Deps.kotlin.test.common
                    implementation Deps.kotlin.test.annotationsCommon
                }
            }
        }
    }
}

void setupAllTargetsWithDefaultSourceSets(Map parameters) {
    setupAllTargetsWithDefaultSourceSets(
            parameters['project'],
            parameters.getOrDefault('isLinuxArm32HfpEnabled', true)
    )
}

void setupAllTargetsWithDefaultSourceSets(Project project, Boolean isLinuxArm32HfpEnabled = true) {
    if (Target.shouldDefineTarget(project, Target.ALL_LINUX_HOSTED)) {
        setupAndroidTarget(project)
        setupJvmTarget(project)
        setupJsTarget(project)
        setupLinuxX64Target(project)
        if (isLinuxArm32HfpEnabled) {
            setupLinuxArm32HfpTarget(project)
        }

        project.kotlin {
            sourceSets {
                jvmJsCommonMain.dependsOn commonMain
                jvmJsCommonTest.dependsOn commonTest

                jvmNativeCommonMain.dependsOn commonMain
                jvmNativeCommonTest.dependsOn commonTest

                jvmCommonMain.dependsOn jvmJsCommonMain
                jvmCommonMain.dependsOn jvmNativeCommonMain
                jvmCommonTest.dependsOn jvmJsCommonTest
                jvmCommonTest.dependsOn jvmNativeCommonTest

                jvmMain.dependsOn jvmCommonMain
                jvmTest.dependsOn jvmCommonTest

                androidMain.dependsOn jvmCommonMain
                androidTest.dependsOn jvmCommonTest

                jsMain.dependsOn jvmJsCommonMain
                jsTest.dependsOn jvmJsCommonTest

                nativeCommonMain.dependsOn jvmNativeCommonMain
                nativeCommonTest.dependsOn jvmNativeCommonTest

                linuxCommonMain.dependsOn nativeCommonMain
                linuxCommonTest.dependsOn nativeCommonTest

                linuxX64Main.dependsOn linuxCommonMain
                linuxX64Test.dependsOn linuxCommonTest

                if (isLinuxArm32HfpEnabled) {
                    linuxArm32HfpMain.dependsOn linuxCommonMain
                    linuxArm32HfpTest.dependsOn linuxCommonTest
                }
            }
        }
    }

    if (Target.shouldDefineTarget(project, Target.ALL_MACOS_HOSTED)) {
        setupIosTargets(project)

        project.kotlin {
            sourceSets {
                jvmNativeCommonMain.dependsOn commonMain
                jvmNativeCommonTest.dependsOn commonTest

                nativeCommonMain.dependsOn jvmNativeCommonMain
                nativeCommonTest.dependsOn jvmNativeCommonTest

                darwinCommonMain.dependsOn nativeCommonMain
                darwinCommonTest.dependsOn nativeCommonTest

                watchosArm32Main.dependsOn darwinCommonMain
                watchosArm32Test.dependsOn darwinCommonTest

                watchosArm64Main.dependsOn darwinCommonMain
                watchosArm64Test.dependsOn darwinCommonTest

                watchosSimMain.dependsOn darwinCommonMain
                watchosSimTest.dependsOn darwinCommonTest

                tvosArm64Main.dependsOn darwinCommonMain
                tvosArm64Test.dependsOn darwinCommonTest

                tvosSimMain.dependsOn darwinCommonMain
                tvosSimTest.dependsOn darwinCommonTest

                macosX64Main.dependsOn darwinCommonMain
                macosX64Test.dependsOn darwinCommonTest

                ios32Main.dependsOn darwinCommonMain
                ios32Test.dependsOn darwinCommonTest

                ios64Main.dependsOn darwinCommonMain
                ios64Test.dependsOn darwinCommonTest

                iosSimMain.dependsOn darwinCommonMain
                iosSimTest.dependsOn darwinCommonTest
            }
        }
    }
}

private void setupAndroidTarget(Project project) {
    if (!Target.shouldDefineTarget(project, Target.JVM)) {
        return
    }

    project.apply plugin: 'com.android.library'

    project.android {
        buildToolsVersion '29.0.2'
        compileSdkVersion 29

        defaultConfig {
            minSdkVersion 1
            targetSdkVersion 29
        }
    }

    project.kotlin {
        targets.fromPreset(presets.android, 'android')

        sourceSets {
            androidMain {
                dependencies {
                    implementation Deps.kotlin.stdlib
                }
            }

            androidTest {
                dependencies {
                    implementation Deps.kotlin.test.junit
                }
            }
        }

        android {
            publishLibraryVariants('release', 'debug')
        }
    }
}

private void setupJvmTarget(Project project) {
    if (!Target.shouldDefineTarget(project, Target.JVM)) {
        return
    }

    project.kotlin {
        targets.fromPreset(presets.jvm, 'jvm')

        sourceSets {
            jvmMain {
                dependencies {
                    implementation Deps.kotlin.stdlib
                }
            }

            jvmTest {
                dependencies {
                    implementation Deps.kotlin.test.junit
                }
            }
        }
    }
}


private void setupJsTarget(Project project) {
    project.apply plugin: JsPlugin
}

private void setupLinuxX64Target(Project project) {
    if (!Target.shouldDefineTarget(project, Target.LINUX)) {
        return
    }

    project.kotlin {
        targets {
            fromPreset(presets.linuxX64, 'linuxX64')
        }
    }
}

private void setupLinuxArm32HfpTarget(Project project) {
    if (!Target.shouldDefineTarget(project, Target.LINUX)) {
        return
    }

    project.kotlin {
        targets {
            fromPreset(presets.linuxArm32Hfp, 'linuxArm32Hfp')
        }
    }
}

private void setupIosTargets(Project project) {
    project.apply plugin: DarwinPlugin
}
